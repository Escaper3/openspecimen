import org.apache.tools.ant.filters.ReplaceTokens;

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

sourceSets {
  main.java.srcDirs = ['WEB-INF/src']
  main.resources.srcDirs = ['WEB-INF/resources']
}

group = 'com.krishagni'

description = "openspecimen"

def env = 'dev';
def envScript;

if (project.hasProperty("environment")) {
  env = environment
}

if (env == 'prod') {
  envScript = file("prod.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $environment")
  }
} else if (env == 'test') {
  envScript = file("test.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $environment")
  }
} else {
  envScript = file("dev.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $environment")
  }
}

repositories {
  maven { url "http://repo.maven.apache.org/maven2" }
}

configurations {
  ajc
  aspects
  aspectCompile
  
  compile {
    extendsFrom aspects
  }
}

configurations.all {
  transitive = false
}

dependencies {
  ajc       "org.aspectj:aspectjtools:1.6.6"
  compile   "org.aspectj:aspectjrt:1.6.6"
  aspects   "org.springframework:spring-aspects:2.5.1"

  compile fileTree(dir: 'lib', include: ['DynamicExtensions.jar', 'kxml2-min-2.3.0.jar'])
  
  providedCompile group: 'javax.servlet', name: 'servlet-api', version:'2.5'
  
  compile group: 'antlr', name: 'antlr', version:'2.7.7'
  compile group: 'org.antlr', name: 'antlr4', version:'4.1'
  compile group: 'aopalliance', name: 'aopalliance', version:'1.0'
  compile group: 'asm', name: 'asm', version:'3.0'
  compile group: 'asm', name: 'asm-tree', version:'3.0'
  compile group: 'org.aspectj', name: 'aspectjweaver', version:'1.6.8'
  compile group: 'axis', name: 'axis', version:'1.2-RC2'
  compile group: 'backport-util-concurrent', name: 'backport-util-concurrent', version:'2.1'
  compile group: 'cglib', name: 'cglib', version:'3.1'
  compile group: 'commons-beanutils', name: 'commons-beanutils', version:'1.8.3'
  compile group: 'commons-codec', name: 'commons-codec', version:'1.3'
  compile group: 'commons-collections', name: 'commons-collections', version:'3.2'
  compile group: 'commons-dbcp', name: 'commons-dbcp', version:'1.3'
  compile group: 'org.apache.commons', name: 'commons-digester3', version:'3.0'
  compile group: 'commons-fileupload', name: 'commons-fileupload', version:'1.2.1'
  compile group: 'commons-httpclient', name: 'commons-httpclient', version:'3.0.1'
  compile group: 'commons-io', name: 'commons-io', version:'2.4'
  compile group: 'commons-lang', name: 'commons-lang', version:'2.3'
  compile group: 'commons-logging', name: 'commons-logging', version:'1.0.4'
  compile group: 'net.sf.ehcache', name: 'ehcache', version:'1.2.3'
  compile group: 'com.google.code.gson', name: 'gson', version:'2.2.2'
  compile group: 'org.hibernate', name: 'hibernate-commons-annotations', version:'3.2.0.Final'
  compile group: 'org.hibernate', name: 'hibernate-core', version:'4.2.18.Final'
  compile group: 'org.hibernate', name: 'hibernate-envers', version:'4.2.18.Final'
  compile group: 'com.itextpdf', name: 'itextpdf', version:'5.4.0'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.2.0'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.2.0'
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version:'1.9.2'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.2.0'
  compile group: 'org.codehaus.jackson', name: 'jackson-jaxrs', version:'1.9.2'
  compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version:'1.9.2'
  compile group: 'org.codehaus.jackson', name: 'jackson-xc', version:'1.9.2'
  compile group: 'oro', name: 'oro', version:'2.0.8'
  compile group: 'org.javassist', name: 'javassist', version:'3.18.1-GA'
  compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version:'1.0.6'
  compile group: 'com.sun.xml.bind', name: 'jaxb-libs', version:'1.0.6'
  compile group: 'com.sun.xml.bind', name: 'jaxb-xjc', version:'1.0.6'
  compile group: 'org.jboss.logging', name: 'jboss-logging', version:'3.1.1.GA'
  compile group: 'org.scijava', name: 'jep', version:'2.4.2'
  compile group: 'com.sun.jersey', name: 'jersey-bundle', version:'1.8'
  compile group: 'org.json', name: 'json', version:'20140107'
  compile group: 'javax.ws.rs', name: 'jsr311-api', version:'1.1.1'
  compile group: 'javax.servlet', name: 'jstl', version:'1.0.5'
  compile group: 'org.liquibase', name: 'liquibase-core', version:'3.3.2'
  compile group: 'log4j', name: 'log4j', version:'1.2.16'
  compile group: 'org.apache.lucene', name: 'lucene-core', version:'2.0.0'
  compile group: 'javax.mail', name: 'mail', version:'1.4.5'
  compile group: 'net.sf.opencsv', name: 'opencsv', version:'1.8'
  compile group: 'quartz', name: 'quartz', version:'1.5.2'
  compile group: 'org.springframework', name: 'spring-aop', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-beans', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-context', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-context-support', version:'3.1.2.RELEASE'
  compile group: 'org.springframework', name: 'spring-core', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-expression', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-instrument', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-jdbc', version:'3.2.5.RELEASE'
  compile group: 'org.springframework.ldap', name: 'spring-ldap-core', version:'2.0.2.RELEASE'
  compile group: 'org.springframework', name: 'spring-orm', version:'3.2.5.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-config', version:'3.2.4.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-core', version:'3.2.0.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-ldap', version:'3.2.5.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-web', version:'3.2.4.RELEASE'
  compile group: 'org.springframework', name: 'spring-tx', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-web', version:'3.2.5.RELEASE'
  compile group: 'org.springframework', name: 'spring-webmvc', version:'3.2.5.RELEASE'
  compile group: 'velocity', name: 'velocity-dep', version:'1.4'
  compile group: 'com.thoughtworks.xstream', name: 'xstream', version:'1.4.4'
  compile group: 'com.thoughtworks.xstream', name: 'xstream-hibernate', version:'1.4.4'
  
  testCompile group: 'junit', name: 'junit', version:'4.11'
}

compileJava {
  sourceCompatibility=sourceCompatibilityValue
  targetCompatibility=targetCompatibilityValue

  doLast{
    ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", 
                classpath: configurations.ajc.asPath)
      
    ant.iajc(
      source: sourceCompatibilityValue, 
      target: targetCompatibilityValue, 
      destDir: sourceSets.main.output.classesDir.absolutePath, 
      maxmem: "512m", 
      fork: "true",
      aspectPath: configurations.aspects.asPath,  
      sourceRootCopyFilter: "**/*.java",
      classpath: "${configurations.compile.asPath};${configurations.aspectCompile.asPath}"){
      sourceroots{
        sourceSets.main.java.srcDirs.each{
          pathelement(location:it.absolutePath)
        }
      }
    }
  }
}

processResources {
  from(sourceSets.main.resources.srcDirs) {
    include 'application.properties'
    
    def datasource_dialect = (database_type == "mysql") ? datasource_mysql_dialect : datasource_oracle_dialect
    filter(ReplaceTokens, tokens: [datasource_jndi : datasource_jndi, datasource_type : datasource_type, 
                                   datasource_dialect : datasource_dialect])
  }
}

war {
  exclude 'WEB-INF/src'
  exclude 'WEB-INF/test'
  exclude 'jss'
  
  from ('./') {
    include 'ngweb/**'
    include 'WEB-INF/'
  }
  
  from ('./detemp/') {
    include 'csd_web/**'
  }
  
  from ('./www/dist/') {
    include '**/**'
  }
}

build.dependsOn ':grunt_project:grunt_build'

apply from: envScript
