import org.apache.tools.ant.filters.ReplaceTokens;
import org.gradle.api.tasks.Exec;

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'

project.ext["datasource_mysql_dialect"]  = "org.hibernate.dialect.MySQLDialect"
project.ext["datasource_oracle_dialect"] = "org.hibernate.dialect.Oracle10gDialect"
project.ext["mysql_driver"]              = "org.gjt.mm.mysql.Driver"
project.ext["oracle_driver"]             = "oracle.jdbc.OracleDriver"

Properties props = new Properties()
props.load(new FileInputStream("build.properties"))

project.ext["datasource_jndi"]   = props.datasource_jndi
project.ext["deployment_type"]   = props.deployment_type
project.ext["database_type"]     = props.database_type
project.ext["database_host"]     = props.database_host
project.ext["database_port"]     = props.database_port
project.ext["database_name"]     = props.database_name
project.ext["database_username"] = props.database_username
project.ext["database_password"] = props.database_password

def springVersion = "3.2.13.RELEASE"

def deploymentLocation = props.appHome + "/server/default/deploy"

if(new File(props.appHome + '/webapps').exists()) {
  deploymentLocation = props.appHome + "/webapps"
}

project.ext["deploy_location"] = deploymentLocation

def environment = 'dev';
def envScript;

if (project.hasProperty("env")) {
  environment = env
}

if (environment == 'prod') {
  envScript = file("prod.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $env")
  }
} else if (environment == 'test') {
  envScript = file("test.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $env")
  }
} else {
  envScript = file("dev.gradle")
  if (!envScript.exists()) {
    throw new GradleException("Unknown environment: $env")
  }
}

sourceSets {
  main.java.srcDirs = ['WEB-INF/src']
  main.resources.srcDirs = ['WEB-INF/resources']
}

description = "openspecimen"

repositories {
  maven { url "http://repo.maven.apache.org/maven2" }
}

configurations {
  ajc
  aspects
  aspectCompile
  
  compile {
    extendsFrom aspects
  }
}

configurations.all {
  transitive = false
}

dependencies {
  ajc       "org.aspectj:aspectjtools:1.6.6"
  compile   "org.aspectj:aspectjrt:1.6.6"
  aspects   "org.springframework:spring-aspects:2.5.1"

  compile fileTree(dir: 'lib', include: ['DynamicExtensions.jar', 'kxml2-min-2.3.0.jar'])
  
  providedCompile group: 'javax.servlet', name: 'servlet-api', version:'2.5'
  
  compile group: 'org.antlr', name: 'antlr4', version:'4.5'
  compile group: 'aopalliance', name: 'aopalliance', version:'1.0'
  compile group: 'asm', name: 'asm', version:'3.3.1'
  compile group: 'org.aspectj', name: 'aspectjweaver', version:'1.6.8'
  compile group: 'backport-util-concurrent', name: 'backport-util-concurrent', version:'2.1'
  compile group: 'cglib', name: 'cglib', version:'3.1'
  compile group: 'commons-beanutils', name: 'commons-beanutils', version:'1.9.2'
  compile group: 'commons-codec', name: 'commons-codec', version:'1.10'
  compile group: 'commons-collections', name: 'commons-collections', version:'3.2.1'
  compile group: 'commons-dbcp', name: 'commons-dbcp', version:'1.4'
  compile group: 'org.apache.commons', name: 'commons-digester3', version:'3.2'
  compile group: 'commons-fileupload', name: 'commons-fileupload', version:'1.3.1'
  compile group: 'commons-httpclient', name: 'commons-httpclient', version:'3.1'
  compile group: 'commons-io', name: 'commons-io', version:'2.4'
  compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
  compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
  compile group: 'net.sf.ehcache', name: 'ehcache', version:'1.2.3'
  compile group: 'com.google.code.gson', name: 'gson', version:'2.3.1'
  compile group: 'org.hibernate', name: 'hibernate-commons-annotations', version:'3.2.0.Final'
  compile group: 'org.hibernate', name: 'hibernate-core', version:'4.2.18.Final'
  compile group: 'org.hibernate', name: 'hibernate-envers', version:'4.2.18.Final'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.5.3'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.5.3'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.5.3'
  compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version:'1.9.13'
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version:'1.9.13'
  compile group: 'org.javassist', name: 'javassist', version:'3.19.0-GA'
  compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version:'2.2.11'
  compile group: 'com.sun.xml.bind', name: 'jaxb-libs', version:'1.0.6'
  compile group: 'com.sun.xml.bind', name: 'jaxb-xjc', version:'2.2.11'
  compile group: 'org.jboss.logging', name: 'jboss-logging', version:'3.1.1.GA'
  compile group: 'org.scijava', name: 'jep', version:'2.4.2'
  compile group: 'org.json', name: 'json', version:'20140107'
  compile group: 'javax.ws.rs', name: 'jsr311-api', version:'1.1.1'
  compile group: 'javax.servlet', name: 'jstl', version:'1.2'
  compile group: 'org.liquibase', name: 'liquibase-core', version:'3.3.3'
  compile group: 'log4j', name: 'log4j', version:'1.2.16'
  compile group: 'org.apache.lucene', name: 'lucene-core', version:'5.1.0'
  compile group: 'javax.mail', name: 'mail', version:'1.4.7'
  compile group: 'net.sf.opencsv', name: 'opencsv', version:'2.3'
  compile group: 'quartz', name: 'quartz', version:'1.5.2'
  compile group: 'org.springframework', name: 'spring-aop', version:springVersion
  compile group: 'org.springframework', name: 'spring-beans', version:springVersion
  compile group: 'org.springframework', name: 'spring-context', version:springVersion
  compile group: 'org.springframework', name: 'spring-context-support', version:'3.1.2.RELEASE'
  compile group: 'org.springframework', name: 'spring-core', version:springVersion
  compile group: 'org.springframework', name: 'spring-expression', version:springVersion
  compile group: 'org.springframework', name: 'spring-instrument', version:springVersion
  compile group: 'org.springframework', name: 'spring-jdbc', version:springVersion
  compile group: 'org.springframework', name: 'spring-tx', version:springVersion
  compile group: 'org.springframework', name: 'spring-web', version:springVersion
  compile group: 'org.springframework', name: 'spring-webmvc', version:springVersion
  compile group: 'org.springframework', name: 'spring-orm', version:springVersion
  compile group: 'org.springframework.ldap', name: 'spring-ldap-core', version:'2.0.3.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-config', version:'3.2.7.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-core', version:'3.2.7.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-ldap', version:'3.2.7.RELEASE'
  compile group: 'org.springframework.security', name: 'spring-security-web', version:'3.2.7.RELEASE'
  compile group: 'velocity', name: 'velocity-dep', version:'1.4'
  compile group: 'com.thoughtworks.xstream', name: 'xstream', version:'1.4.8'
  
  testCompile group: 'junit', name: 'junit', version:'4.11'
}

compileJava {
  doLast{
    ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", 
                classpath: configurations.ajc.asPath)
      
    ant.iajc(
      source: 1.6, 
      target: 1.6, 
      destDir: sourceSets.main.output.classesDir.absolutePath, 
      maxmem: "512m", 
      fork: "true",
      aspectPath: configurations.aspects.asPath,  
      sourceRootCopyFilter: "**/*.java",
      classpath: "${configurations.compile.asPath};${configurations.aspectCompile.asPath}"){
      sourceroots{
        sourceSets.main.java.srcDirs.each{
          pathelement(location:it.absolutePath)
        }
      }
    }
  }
}

processResources {
  from(sourceSets.main.resources.srcDirs) {
    include 'application.properties'
    
    def datasource_dialect = (database_type == "mysql") ? datasource_mysql_dialect : datasource_oracle_dialect
    filter(ReplaceTokens, tokens: [datasource_jndi : datasource_jndi, datasource_type : deployment_type, 
                                   datasource_dialect : datasource_dialect])
  }
}

task unzip_de(type: Copy) {
  def zipFile = file('./dynamicextensions.zip')
  def outputDir = file("${buildDir}/detemp")
  
  from zipTree(zipFile) 
  into outputDir
}

war {
  from ('./') {
    include 'ngweb/**'
  }
  
  from ('./www/dist/') {
    include '**/**'
  }
  
  from ('./WEB-INF/') {
    include 'integration-test/**'
    include 'resources/**'
    include 'applicationContext.xml'
    include 'restApiContext.xml'
    include 'web.xml'
    
    into 'WEB-INF/'
  }
  
  from ('./build/detemp/') {
    include 'csd_web/**'
  }
  
  from ('./build/detemp/ngweb/') {
    include 'css/**'
    include 'js/**'
    
    into '/ngweb/external/de'
  }
}

task grunt_build(type: Exec) {
   workingDir './www'
   commandLine 'grunt', 'build'
}

war.dependsOn unzip_de
build.dependsOn grunt_build

apply from: envScript
